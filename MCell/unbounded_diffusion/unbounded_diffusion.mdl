/* unbounded diffusion */

/* simple numerical variables */
        red_ligand_diffusion_constant = 6.0e-6
        number_of_ligand_molecules = 1.0e4
        release_site_diameter = 0
/* simple numerical variables, numerical expressions, and numerical arrays */
        red_xy_value = 0.0
        red_location = [red_xy_value, red_xy_value, 0.0]
        blue_x_value = red_xy_value-1.0
        blue_location = [blue_x_value, 0.0-1.0, 0.0]
        blue_ligand_diffusion_constant = red_ligand_diffusion_constant/2.0
/* simple numerical variables */
        dt = 1.0e-6     /* exponential notation: 1.0e-6 = 0.000001 */
        iterations = 300

TIME_STEP = dt
ITERATIONS = iterations

DEFINE_MOLECULE red_ligand {DIFFUSION_CONSTANT = red_ligand_diffusion_constant}
DEFINE_MOLECULE blue_ligand {DIFFUSION_CONSTANT = blue_ligand_diffusion_constant}

INSTANTIATE microdomain OBJECT {
	red_ligand_release_site SPHERICAL_RELEASE_SITE {
		LOCATION = red_location
		MOLECULE = red_ligand
		NUMBER_TO_RELEASE = number_of_ligand_molecules
		SITE_DIAMETER = release_site_diameter
	}
	blue_ligand_release_site SPHERICAL_RELEASE_SITE {
		LOCATION = blue_location
		MOLECULE = blue_ligand
		NUMBER_TO_RELEASE = number_of_ligand_molecules
		SITE_DIAMETER = release_site_diameter
	}
}

VIZ_OUTPUT {
        MODE = DREAMM_V3_GROUPED
        FILENAME = "./viz_data/tutorial_1"
        MOLECULES {
                NAME_LIST { ALL_MOLECULES }
                ITERATION_NUMBERS { ALL_DATA @ ALL_ITERATIONS }
        }
}

REACTION_DATA_OUTPUT {
	STEP = 2*dt
	{COUNT[red_ligand,WORLD]} => "reaction_data/count.red_ligand"
	{COUNT[blue_ligand,WORLD]} => "reaction_data/count.blue_ligand"
}
